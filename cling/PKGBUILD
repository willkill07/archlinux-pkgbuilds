# cling-git
# Maintainer: willkill07 <_ at willkill07 dot dev>
# Heavily adopted from: archibald869 <archibald869 at web dot de>

pkgname=cling-git
pkgver=r5187.1c6031a0
pkgrel=1
pkgdesc="Interactive C++ interpreter, built on the top of LLVM and Clang libraries"
arch=("x86_64")
url="https://root.cern.ch/cling"
license=("custom:Cling Release License")
provides=("cling")
conflicts=("cling")
depends=("libxml2")
makedepends=("git" "cmake" "ninja" "python")
optdepends=(
    "perl: support for scan-build, ccc-analyzer and c++-analyzer"
    "python: support for scan-view and Jupyter"
    "python-pygments: support for opt-viewer"
    "python-yaml: support for opt-viewer"
)
source=(
    "cling-llvm::git+http://github.com/root-project/llvm-project.git#branch=cling-latest"
    "cling::git+http://github.com/root-project/cling.git"
)
sha256sums=(
    "SKIP"
    "SKIP"
)
options=('!lto')

pkgver() {
    cd "$srcdir/cling"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    # there's an upstream bug that incorrectly checks if we are in/out of llvm tree
    sed -i '/target_compile_definitions(clingInterpreter PUBLIC/d' \
	"$srcdir/cling/lib/Interpreter/CMakeLists.txt"
    
    cmake \
	-B "$srcdir/build" \
	-S "$srcdir/cling-llvm/llvm" \
	-G Ninja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX="/opt/cling" \
	-DLLVM_ENABLE_PROJECTS="clang" \
	-DLLVM_ENABLE_RUNTIMES="pstl" \
	-DLLVM_EXTERNAL_PROJECTS="cling" \
	-DLLVM_EXTERNAL_CLING_SOURCE_DIR="$srcdir/cling" \
	-DLLVM_TARGETS_TO_BUILD="X86" \
	-DLLVM_EXPERIMENTAL_TARGETS_TO_BUILD="NVPTX"
    
    cmake --build "$srcdir/build"
}

package() {
    cd "$srcdir/build"
    DESTDIR="$pkgdir" ninja install
    install -d "$pkgdir/usr/bin"
    ln -s "/opt/cling/bin/cling" "$pkgdir/usr/bin/cling"
}
